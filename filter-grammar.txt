
unichar     ::= any unicode character
stringchar  ::= unichar - ('"' | "\")
wordchar    ::= stringchar - (" " | "=" | "(" | ")" | "!" | "&" | "|")
reserved    ::= not | and | or

string      ::= '"' {stringchar | escape} '"'
              | word
word        ::= ((wordchar | escape) {wordchar | escape}) - reserved
escape      ::= "\" unichar

filter      ::= [notterm {[operator] notterm}]
notterm     ::= [not] term
term        ::= "(" filter ")"
              | string ["=" string]
operator    ::= and | or

not         ::= "!" | "not" | "NOT"
and         ::= "&" | "and" | "AND"
or          ::= "|" | "or" | "OR"

